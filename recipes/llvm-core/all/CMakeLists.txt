cmake_minimum_required(VERSION 3.15)
project(conanllvm)

if(LLVM_ENABLE_ZLIB)
  find_package(ZLIB REQUIRED CONFIG)

  # The FindZLIB find module provided by CMake behaves differently than the FindZLIB.cmake script generated by CONAN.
  # So the following two lines fill in the needed properties.
  list(GET ZLIB_LIBRARIES 0 ZLIB_LIBRARY)
  set_property(TARGET ZLIB::ZLIB PROPERTY LOCATION "${ZLIB_LIBRARY}")

  # Additionally LLVM 11.1.0 requires the zlib lib dir to be in the library path.
  # This is not needed for later versions and can be removed once
  # LLVM-12 becomes the oldest supported version.
  get_filename_component(ZLIB_LIBRARY_DIR "${ZLIB_LIBRARY}" DIRECTORY)
  if(UNIX)
    set(ENV{LIBRARY_PATH} "${ZLIB_LIBRARY_DIR}:$ENV{LIBRARY_PATH}")
  elseif(WIN32)
    file(TO_NATIVE_PATH "${ZLIB_LIBRARY_DIR}" WINDOWS_ZLIB_PATH)
    string(APPEND CMAKE_EXE_LINKER_FLAGS " /LIBPATH:${WINDOWS_ZLIB_PATH}")
  endif()
endif()

add_subdirectory(src)
